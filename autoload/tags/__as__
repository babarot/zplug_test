#!/bin/zsh

local    arg="$1" package
local -a parsed_zplugs
local    as
local    default="plugin"
local -a candidates

if [[ -n $arg ]] && ! __zplug::base::base::zpluged "$arg"; then
    __zplug::io::print::die \
        "[zplug] $arg: no package managed by zplug\n"
    return 1
fi

candidates=(
"$default"
"command"
)

package="${arg}, ${zplugs[$arg]%, }"
parsed_zplugs=(${(s/, /)package/,  */, })

as="${parsed_zplugs[(k)as:*]#as:*}"
if [[ -z $as ]]; then
    zstyle -t ":zplug:tag" as "${candidates[@]}"
    case $status in
        0)
            # ok
            zstyle -s ":zplug:tag" as as
            ;;
        1)
            __zplug::io::print::die \
                "[zplug] $fg[red]ERROR$reset_color: "
            __zplug::io::print::die \
                "as tag must be [${(j:, :)candidates[@]}] ($fg[green]$arg$reset_color)\n"
            return 1
            ;;
        2)
            # undefined
            ;;
    esac
fi

: ${as:=$default}
if [[ ! $as =~ ^(${(j:|:)candidates[@]})$ ]]; then
    __zplug::io::print::die \
        "[zplug] $fg[red]ERROR$reset_color: "
    __zplug::io::print::die \
        "as tag must be [${(j:, :)candidates[@]}] ($fg[green]$arg$reset_color)\n"
    return 1
fi

echo "$as"
